plugins {
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    group = "io.github.lxgaming"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    configurations {
        compileJar {
            compileOnly.extendsFrom(compileJar)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation("org.jetbrains:annotations:23.0.0")
        testImplementation("org.junit.jupiter:junit-jupiter-engine:5.9.1")
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        failOnError = false
    }

    processResources {
        if (project(":agent") != project) {
            dependsOn("copyResources")
            finalizedBy("mergeResources")
        }
    }

    publishing {
        publications {
            mavenPublication(MavenPublication) {
                groupId group
                artifactId archivesBaseName
                version version
                pom {
                    name = "Agent"
                    url = "https://github.com/LXGaming/Agent"
                    developers {
                        developer {
                            id = "lxgaming"
                            name = "LXGaming"
                        }
                    }
                    issueManagement {
                        system = "GitHub Issues"
                        url = "https://github.com/LXGaming/Agent/issues"
                    }
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    scm {
                        connection = "scm:git:https://github.com/LXGaming/Agent.git"
                        developerConnection = "scm:git:https://github.com/LXGaming/Agent.git"
                        url = "https://github.com/LXGaming/Agent"
                    }
                }
            }
        }
        repositories {
            if (project.hasProperty("sonatypeUsername") && project.hasProperty("sonatypePassword")) {
                maven {
                    name = "sonatype"
                    url = version.contains("-SNAPSHOT") ? "https://s01.oss.sonatype.org/content/repositories/snapshots" : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                    credentials {
                        username project.property("sonatypeUsername")
                        password project.property("sonatypePassword")
                    }
                }
            }
        }
    }

    signing {
        if (project.hasProperty("signingKey") && project.hasProperty("signingPassword")) {
            useInMemoryPgpKeys(project.property("signingKey"), project.property("signingPassword"))
        }

        sign publishing.publications
    }

    test {
        testLogging {
            exceptionFormat = "full"
        }

        useJUnitPlatform()
    }

    task dist(type: Copy) {
        onlyIf {
            project(":agent") != project
        }

        dependsOn("build")
        from jar
        into "${project.rootProject.projectDir}/dist"
    }

    task copyResources(type: Copy) {
        from project(":agent").file("src/main/resources/simplelogger.properties")
        into "$buildDir/resources/main"
    }

    task mergeResources {
        doLast {
            Set<File> files = [
                project(":agent"),
                project
            ].collect { project ->
                project.file("src/main/resources/agent.conf")
            }

            def output = new File("$buildDir/resources/main/agent.conf")
            output.getParentFile().mkdirs()
            output.newWriter().withWriter { writer ->
                writer << files.collect { file -> file.text.normalize() }.join('\n')
            }
        }
    }
}